plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'br.com.activity'
    compileSdk 35

    defaultConfig {
        applicationId "br.com.activity"
        minSdk 23
        targetSdk 35
        versionCode 32
        versionName "1.3.5"
    }

    signingConfigs {
        release {
            def props = new Properties()
            def localPropsFile = rootProject.file("signing.properties")
            if (localPropsFile.exists()) {
                props.load(new FileInputStream(localPropsFile))
            }

            storeFile file(props.getProperty("RELEASE_STORE_FILE"))
            storePassword props.getProperty("RELEASE_STORE_PASSWORD")
            keyAlias props.getProperty("RELEASE_KEY_ALIAS")
            keyPassword props.getProperty("RELEASE_KEY_PASSWORD")
        }
    }


    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "boolean", "ADS_ENABLED", "true"
        }

        withoutAds {
            initWith debug
            buildConfigField "boolean", "ADS_ENABLED", "false"
            applicationIdSuffix ".withoutads"
            versionNameSuffix "-withoutads"
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            buildConfigField "boolean", "ADS_ENABLED", "true"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        checkReleaseBuilds false
        abortOnError false
        baseline = file("lint-baseline.xml")
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.1.21"
    implementation platform('com.google.firebase:firebase-bom:34.0.0')
    implementation("com.google.firebase:firebase-crashlytics-ndk")
    implementation("com.google.firebase:firebase-analytics")

    implementation 'com.google.android.gms:play-services-ads:24.5.0'
}
